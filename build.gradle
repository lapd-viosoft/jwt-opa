// Copyright (c) 2020 kapsules.io.  All rights reserved.
// Created 2020-09-02, M. Massenzio

plugins {
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'jacoco'
}

allprojects {
    group 'io.kapsules'
    version '1.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

// Fail the build if test coverage (by lines covered) falls below 60%.
// See: https://reflectoring.io/jacoco/
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.6
                }
            }
        }
    }
}
